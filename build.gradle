plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'

    // configure integrationTests:
    // https://tomgregory.com/gradle/gradle-integration-tests/#:~:text=Your%20Gradle%20project%20can%20easily,developers%20to%20work%20more%20productively.
    // https://www.youtube.com/watch?v=_q5Rxgtn1vg&t=390s&ab_channel=TomGregoryTech
    id 'org.unbroken-dome.test-sets' version '4.1.0'
}

bootJar {
    enabled = false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.unbroken-dome.test-sets'

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    testSets {
        integrationTest
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.kafka:spring-kafka'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.kafka:spring-kafka-test'

        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    // apply this to ALL tests (unit + integration)
    tasks.withType(Test) {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    // Runs both unit and integration tests
    tasks.named('check') {
        dependsOn tasks.named('integrationTest')
    }
}
